plugins {
    id "java-library"
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "signing"
}

group "com.github.alexdlaird"
version "0.0.1-SNAPSHOT"

def sonatypeUsername = hasProperty("sonatypeUsername") ? sonatypeUsername : System.getenv("SONATYPE_USERNAME")
def sonatypePassword = hasProperty("sonatypePassword") ? sonatypePassword : System.getenv("SONATYPE_PASSWORD")

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
        xml.destination = file("build/reports/jacoco/report.xml")
    }
}

publishing {
    publications {
        javaLibrary(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = "java-ngrok"
                description = "A Java wrapper for ngrok"
                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "https://github.com/alexdlaird/java-ngrok/blob/master/LICENSE"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id = "alexdlaird"
                        name = "Alex Laird"
                        email = "contact@alexlaird.com"
                    }
                }
                scm {
                    url "http://github.com/alexdlaird/java-ngrok"
                    connection "scm:git@github.com:alexdlaird/java-ngrok.git"
                    developerConnection "scm:git@github.com:alexdlaird/java-ngrok.git"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd
    sign publishing.publications.javaLibrary
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
